/*
 * Adapted from The MIT License (MIT)
 *
 * Copyright (c) 2016-2020 DaPorkchop_
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated documentation
 * files (the "Software"), to deal in the Software without restriction, including without limitation the rights to use, copy,
 * modify, merge, publish, distribute, sublicense, and/or sell copies of the Software, and to permit persons to whom the Software
 * is furnished to do so, subject to the following conditions:
 *
 * Any persons and/or organizations using this software must include the above copyright notice and this permission notice,
 * provide sufficient credit to the original authors of the project (IE: DaPorkchop_), as well as provide a link to the original project.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES
 * OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS
 * BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 *
 */

buildscript {
    repositories {
        maven {
            name = "Gradle"
            url = "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "gradle.plugin.com.github.johnrengelman:shadow:7.1.0"
    }
}

apply plugin: "java"
apply plugin: "com.github.johnrengelman.shadow"

ext {
    //general things
    javaVersion = "1.8"
    minecraftVersion = "1.12.2"
    programVersion = "0.2.8"

    //dependency things
    apachecommonstextVersion = "1.3"
    javawebsocketVersion = "1.4.0"
    junitVersion = "4.12"
    lombokVersion = "1.16.20"
    mcprotocollibVersion = "1.12.2-SNAPSHOT"
    porklibVersion = "0.5.7-SNAPSHOT"
    discord4jVersion = "3.2.1"
    pounceVersion = "main-SNAPSHOT"
}

group "com.zenith"

compileJava {
    sourceCompatibility = targetCompatibility = "$javaVersion"
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

repositories {
    maven {
        name = "jitpack.io"
        url = "https://jitpack.io"
    }
    maven {
        name = "DaPorkchop_"
        url = "https://maven.daporkchop.net/"
    }
    maven {
        name = "maven central"
        url = "https://repo1.maven.org/maven2/"
    }
    mavenLocal()
}

configurations {
    shade
    implementation.extendsFrom shade
}

dependencies {
    shade 'com.google.guava:guava:31.1-jre'
    shade "org.apache.commons:commons-math3:3.6.1"
    shade "net.daporkchop.lib:gui:$porklibVersion"
    shade "net.daporkchop.lib:hash:$porklibVersion"
    shade "net.daporkchop.lib:logging:$porklibVersion"
    shade "net.daporkchop.lib:minecraft-text:$porklibVersion"

    shade ("com.github.5cmc:mcprotocollib:$mcprotocollibVersion") {
        exclude group: "io.netty", module: "netty-all"
    }

    shade 'io.netty:netty-codec-haproxy:4.1.70.Final'

    shade "com.discord4j:discord4j-core:$discord4jVersion"

    shade "org.java-websocket:Java-WebSocket:$javawebsocketVersion"

    shade "com.github.collarmc:Pounce:$pounceVersion"

    testImplementation "junit:junit:$junitVersion"

    compileOnly "org.projectlombok:lombok:$lombokVersion"
    testCompileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"
    testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"
    shade ('io.netty:netty-resolver-dns-native-macos:4.1.77.Final:osx-aarch_64')
}

//disable regular unshaded jar
jar.enabled = false

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
}

shadowJar {
    archiveName "${project.name}.jar"

    configurations = [project.configurations.shade]

    exclude 'module-info.class'

    manifest {
        attributes "Implementation-Title": "Proxy",
                "Implementation-Version": project.version,
                "Main-Class": "com.zenith.Proxy"
    }
}

build.dependsOn(shadowJar)

import groovy.json.JsonSlurper

buildscript {
    repositories {
        maven {
            name = "Gradle"
            url = "https://plugins.gradle.org/m2/"
        }
    }
}

plugins {
    // todo: cannot upgrade this as consumers still have jdk 8 configured in gradle separate from the toolchain
    id 'nu.studer.jooq' version '5.2.2'
    id "com.github.johnrengelman.shadow" version "8.1.0"
}

apply plugin: "java"

ext {
    lombokVersion = "1.18.26"
    porklibVersion = "0.5.8-SNAPSHOT"
    postgresVersion = '42.6.0'
}

group "com.zenith"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(20)
    }
}

tasks.withType(JavaCompile).configureEach {
    sourceCompatibility = targetCompatibility = "20"
    options.encoding = "UTF-8"
    options.compilerArgs.addAll(["--enable-preview"])
}

repositories {
    maven {
        name = "jitpack.io"
        url = "https://jitpack.io"
    }
    maven {
        name = "maven central"
        url = "https://repo1.maven.org/maven2/"
    }
    maven {
        name = "DaPorkchop_"
        url = "https://maven.daporkchop.net/"
    }
    maven {
        name = "litarvan"
        url = "https://litarvan.github.io/maven"
    }
    maven {
        name = "minecraft"
        url "https://libraries.minecraft.net"
    }
    maven {
        "opencollab-release"
        url "https://repo.opencollab.dev/maven-releases/"
    }
    mavenLocal()
}

sourceSets {
    main {
        resources {
            srcDirs("data/")
        }
    }
}

configurations {
    shade
    implementation.extendsFrom shade
}

jooq {
    version = "3.18.0"
    // read from config.json (will fail if config doesn't exist)
    def dbUrl = ''
    def dbUser = ''
    def dbPass = ''
    try {
        def inputFile = new File(projectDir.getAbsolutePath() + "/config.json")
        def jsonFile = new JsonSlurper().parseText(inputFile.text)
        dbUrl = 'jdbc:postgresql://' + jsonFile.database.host + ':' + jsonFile.database.port + '/postgres'
        dbUser = jsonFile.database.username
        dbPass = jsonFile.database.password
    } catch (Exception ex) {
        // fall through
        println("config.json does not exist, unable to set database credentials. generateJooq will fail if run.")
    }

    configurations {
        main {  // name of the jOOQ configuration
            generateSchemaSourceOnCompilation = false  // disable auto-execution in build task

            generationTool {
                logging = org.jooq.meta.jaxb.Logging.WARN
                jdbc {
                    driver = 'org.postgresql.Driver'
                    url = dbUrl
                    user = dbUser
                    password = dbPass
                    properties {
                        property {
                            key = 'ssl'
                            value = 'false'
                        }
                    }
                }
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    database {
                        name = 'org.jooq.meta.postgres.PostgresDatabase'
                        inputSchema = 'public'
                        forcedTypes {
                        }
                    }
                    generate {
                        deprecated = false
                        records = true
                        immutablePojos = true
                        fluentSetters = true
                        routines = false
                    }
                    target {
                        packageName = 'com.zenith.database.dto'
                        // you need to manually move these generated files into main src set
                        // only needs to be done on db schema changes
                        directory = 'build/jooq/main'
                    }
                    strategy.name = 'org.jooq.codegen.DefaultGeneratorStrategy'
                }
            }
        }
    }
}

dependencies {
    jooqGenerator "org.postgresql:postgresql:$postgresVersion"
    shade 'org.jooq:jooq:3.18.0'
    shade "com.zaxxer:HikariCP:5.0.1"
    shade "org.postgresql:postgresql:$postgresVersion"
    shade 'org.eclipse.jgit:org.eclipse.jgit:6.5.0.202303070854-r'
    shade 'org.eclipse.jgit:org.eclipse.jgit.ssh.jsch:6.5.0.202303070854-r'
    shade 'com.github.mwiede:jsch:0.2.8'
    shade 'com.google.guava:guava:31.1-jre'
    shade "org.apache.commons:commons-math3:3.6.1"
    shade "org.apache.commons:commons-collections4:4.4"
    shade "net.daporkchop.lib:hash:$porklibVersion"
    shade "net.daporkchop.lib:minecraft-text:$porklibVersion"
    shade "ch.qos.logback:logback-classic:1.4.6"
    shade 'org.slf4j:slf4j-api:2.0.7'
    shade 'org.fusesource.jansi:jansi:2.4.0'
    shade 'com.mojang:brigadier:1.0.18'
    shade 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.14.2'

    shade("com.github.5cmc:MCProtocolLib:c0b123fbd2") {
        exclude group: 'io.netty.incubator'
        exclude group: 'io.netty'
    }
    shade("com.discord4j:discord4j-core:3.2.4") {
        exclude group: 'io.netty'
    }

    shade "com.github.5cmc:MCAuthLib:94b70b772d"

    shade 'io.netty:netty-codec-haproxy:4.1.91.Final'
    shade 'io.netty:netty-codec-dns:4.1.91.Final'
    shade 'io.netty:netty-codec-http2:4.1.91.Final'
    shade 'io.netty:netty-codec-http:4.1.91.Final'
    shade 'io.netty:netty-codec-socks:4.1.91.Final'
    shade 'io.netty:netty-handler-proxy:4.1.91.Final'
    shade 'io.netty:netty-handler:4.1.91.Final'
    shade 'io.netty:netty-resolver-dns:4.1.91.Final'
    shade 'io.netty:netty-transport-classes-epoll:4.1.91.Final'
    shade 'io.netty:netty-transport-native-epoll:4.1.91.Final:linux-x86_64'
    shade 'io.netty:netty-transport-native-unix-common:4.1.91.Final:linux-x86_64'
    shade 'io.netty:netty-resolver-dns-native-macos:4.1.91.Final:osx-aarch_64'

    shade "com.github.collarmc:pounce:0e8492b68e"
    shade('org.redisson:redisson:3.20.1') {
        exclude group: 'io.netty'
    }
    shade 'com.nukkitx.fastutil:fastutil-object-object-maps:8.5.3'
    shade 'com.nukkitx.fastutil:fastutil-int-object-maps:8.5.3'
    shade 'com.nukkitx.fastutil:fastutil-object-int-maps:8.5.3'
    shade 'com.nukkitx.fastutil:fastutil-long-object-maps:8.5.3'

    testImplementation "org.junit.jupiter:junit-jupiter:5.9.2"

    compileOnly "org.projectlombok:lombok:$lombokVersion"
    testCompileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"
    testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"
}

test {
    useJUnitPlatform()
}

tasks.register('run', JavaExec) {
    group = "run"
    description = "Execute proxy"
    classpath = sourceSets.main.runtimeClasspath
    mainClass.set("com.zenith.Proxy")
    jvmArgs = ["-server",
               "-XX:MaxRAMPercentage=30", "-XX:MinRAMPercentage=30", // optimizing these for 3 instances per machine with varying amounts of ram
               "-XX:+UseG1GC", "-XX:+ParallelRefProcEnabled", "-XX:MaxGCPauseMillis=200", "-XX:+UnlockExperimentalVMOptions",
               "-XX:+AlwaysPreTouch", "-XX:G1NewSizePercent=30", "-XX:G1MaxNewSizePercent=40", "-XX:G1HeapRegionSize=8M",
               "-XX:G1ReservePercent=20", "-XX:G1HeapWastePercent=5", "-XX:G1MixedGCCountTarget=4", "-XX:InitiatingHeapOccupancyPercent=15",
               "-XX:G1MixedGCLiveThresholdPercent=90", "-XX:G1RSetUpdatingPauseTimePercent=5", "-XX:SurvivorRatio=32",
               "-XX:+PerfDisableSharedMem", "-XX:MaxTenuringThreshold=1",
               "-Djdk.virtualThreadScheduler.parallelism=4", "-Djava.util.concurrent.ForkJoinPool.common.parallelism=16", "--enable-preview"]
}

tasks.withType(JavaExec).configureEach {
    javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
}

//disable regular unshaded jar
jar.enabled = false

tasks.register('sourceJar', Jar) {
    from sourceSets.main.allSource
}

shadowJar {
    archiveBaseName.set(project.name)
    archiveClassifier.set('')
    archiveVersion.set('')

    configurations = [project.configurations.shade]

    exclude 'module-info.class'

    manifest {
        attributes "Implementation-Title": "Proxy",
                "Implementation-Version": project.version,
                "Main-Class": "com.zenith.Proxy"
    }
}

tasks.register('clearJooqBuild') {
    // clear any jooq generated classes in the build directory
    // we already have these classes in our src
    // can't configure jooq generator to not auto-add these generated dirs to gradle source sets
    group = "build"
    // this can be removed after all instances get updated to this commit at least once
    new File('build/generated-src').deleteDir()
    new File('build/jooq').deleteDir()
}

compileJava.dependsOn([clearJooqBuild])
tasks.named('generateJooq').configure { allInputsDeclared = false }
tasks.register('jarBuild') {
    group = "build"
}
jarBuild.dependsOn([shadowJar, build])

//build.dependsOn(shadowJar)

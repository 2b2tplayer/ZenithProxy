buildscript {
    repositories {
        maven {
            name = "Gradle"
            url = "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "gradle.plugin.com.github.johnrengelman:shadow:7.1.2"
    }
}

plugins {
    id 'nu.studer.jooq' version '5.2.2'
}

apply plugin: "java"
apply plugin: "com.github.johnrengelman.shadow"

ext {
    //general things
    minecraftVersion = "1.12.2"
    programVersion = "1.0"

    //dependency things
    junitVersion = '4.13.2'
    lombokVersion = "1.18.24"
    mcprotocollibVersion = "7d846e1fa7"
    porklibVersion = "0.5.8-SNAPSHOT"
    discord4jVersion = "3.2.3"
    pounceVersion = "main-SNAPSHOT"
    hikaricpVersion = "4.0.3"
    postgresVersion = "42.3.3"
}

group "com.zenith"

compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

repositories {
    maven {
        name = "jitpack.io"
        url = "https://jitpack.io"
    }
    maven {
        name = "maven central"
        url = "https://repo1.maven.org/maven2/"
    }
    maven {
        name = "DaPorkchop_"
        url = "https://maven.daporkchop.net/"
    }
    maven {
        name = "litarvan"
        url = "https://litarvan.github.io/maven"
    }
    mavenLocal()
}

configurations {
    shade
    implementation.extendsFrom shade
}

jooq {
    configurations {
        main {  // name of the jOOQ configuration
            generateSchemaSourceOnCompilation = true  // default (can be omitted)

            generationTool {
                jdbc {
                    driver = 'org.postgresql.Driver'
                    url = 'jdbc:postgresql://mc-proxy.czyfkzmxjkdp.us-east-1.rds.amazonaws.com:5432/postgres'
                    user = 'proxy'
                    password = 'cghRVLQQiRqEpn9ccJEEeU'
                    properties {
                        property {
                            key = 'ssl'
                            value = 'false'
                        }
                    }
                }
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    database {
                        name = 'org.jooq.meta.postgres.PostgresDatabase'
                        inputSchema = 'public'
                        forcedTypes {
                            forcedType {
                                name = 'varchar'
                                includeExpression = '.*'
                                includeTypes = 'JSONB?'
                            }
                            forcedType {
                                name = 'varchar'
                                includeExpression = '.*'
                                includeTypes = 'INET'
                            }
                        }
                    }
                    generate {
                        deprecated = false
                        records = true
                        immutablePojos = true
                        fluentSetters = true
                    }
                    target {
                        packageName = 'com.zenith.database.dto'
                        directory = 'build/generated-src/jooq/main'  // default (can be omitted)
                    }
                    strategy.name = 'org.jooq.codegen.DefaultGeneratorStrategy'
                }
            }
        }
    }
}

dependencies {
    jooqGenerator "org.postgresql:postgresql:$postgresVersion"
    shade "org.jooq:jooq:3.16.6"
    shade "com.zaxxer:HikariCP:$hikaricpVersion"
    shade "org.postgresql:postgresql:$postgresVersion"
    shade 'org.eclipse.jgit:org.eclipse.jgit:5.13.1.202206130422-r'
    shade 'org.eclipse.jgit:org.eclipse.jgit.ssh.jsch:5.13.1.202206130422-r'
    shade 'com.github.mwiede:jsch:0.2.4'
    shade 'com.google.guava:guava:31.1-jre'
    shade "org.apache.commons:commons-math3:3.6.1"
    shade "net.daporkchop.lib:gui:$porklibVersion"
    shade "net.daporkchop.lib:hash:$porklibVersion"
    shade "net.daporkchop.lib:logging:$porklibVersion"
    shade "net.daporkchop.lib:minecraft-text:$porklibVersion"
    shade "ch.qos.logback:logback-classic:1.3.3"
    shade 'org.slf4j:slf4j-api:2.0.3'
    shade 'org.fusesource.jansi:jansi:2.4.0'

    shade("com.github.5cmc:mcprotocollib:$mcprotocollibVersion") {
        exclude group: "io.netty", module: "netty-all"
    }

    shade 'io.netty:netty-codec-haproxy:4.1.79.Final'

    shade "com.discord4j:discord4j-core:$discord4jVersion"

    shade "com.github.collarmc:Pounce:$pounceVersion"

    testImplementation "junit:junit:$junitVersion"

    compileOnly "org.projectlombok:lombok:$lombokVersion"
    testCompileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"
    testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"
    shade ('io.netty:netty-resolver-dns-native-macos:4.1.79.Final:osx-aarch_64')
}

//disable regular unshaded jar
jar.enabled = false

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
}

shadowJar {
    archiveName "${project.name}.jar"

    configurations = [project.configurations.shade]

    exclude 'module-info.class'

    manifest {
        attributes "Implementation-Title": "Proxy",
                "Implementation-Version": project.version,
                "Main-Class": "com.zenith.Proxy"
    }
}

build.dependsOn(shadowJar)
